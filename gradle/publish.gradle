allprojects {
	repositories {
        mavenCentral()
    }
}

subprojects { project ->
    apply plugin: 'maven'

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    // add javadoc/source jar tasks as artifacts
    artifacts {
        archives sourcesJar, javadocJar
    }

    ext {
        bintrayUser = project.hasProperty('bintrayUser') ? bintrayUser : ''
        bintrayKey = project.hasProperty('bintrayKey') ? bintrayKey : ''
    }

    uploadArchives {
        repositories.mavenDeployer {
            repository(url: "https://api.bintray.com/maven/blackrush/maven/fungsi") {
                authentication(userName: bintrayUser, password: bintrayKey)
            }

            if (project != rootProject) {
                pom.artifactId = "fungsi-${project.name}"
            }
        }
    }
}
